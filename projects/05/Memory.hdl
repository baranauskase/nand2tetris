// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    And(a=address[13], b=address[14], out=isKBDAdr);
    Not(in=isKBDAdr, out=isNotKBDAdr);

    And(a=isNotKBDAdr, b=address[14], out=isScreenAdr);
    And(a=isScreenAdr, b=address[12], out=isScreenLower);
    Not(in=isScreenLower, out=isNotScreenLower);
    And(a=isScreenAdr, b=isNotScreenLower, out=isScreenUpper);

    Or(a=isScreenUpper, b=isKBDAdr, out=isSel0);
    Or(a=isScreenLower, b=isKBDAdr, out=isSel1);

    DMux4Way(in=load, sel[0]=isSel0, sel[1]=isSel1, a=loadData, b=loadScreen1, c=loadScreen2, d=loadKBD);
    
    Or(a=loadScreen1, b=loadScreen2, out=loadScreen);
    RAM16K(in=in, load=loadData, address=address[0..13], out=outData);
    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);
    Keyboard(out=outKBD);

    Mux4Way16(a=outData, b=outScreen, c=outScreen, d=outKBD, sel[0]=isSel0, sel[1]=isSel1, out=out);

}