// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // transform X
    Not(in=zx, out=negzx);
    And16(a=x, b[0]=negzx, b[1]=negzx, b[2]=negzx, b[3]=negzx, b[4]=negzx, b[5]=negzx, b[6]=negzx, b[7]=negzx, b[8]=negzx, b[9]=negzx, b[10]=negzx, b[11]=negzx, b[12]=negzx, b[13]=negzx, b[14]=negzx, b[15]=negzx, out=xafterzx);

    Not16(in=xafterzx, out=xafterneg);
    Mux16(a=xafterzx, b=xafterneg, sel=nx, out=xtransformed);

    // transform y
    Not(in=zy, out=negzy);
    And16(a=y, b[0]=negzy, b[1]=negzy, b[2]=negzy, b[3]=negzy, b[4]=negzy, b[5]=negzy, b[6]=negzy, b[7]=negzy, b[8]=negzy, b[9]=negzy, b[10]=negzy, b[11]=negzy, b[12]=negzy, b[13]=negzy, b[14]=negzy, b[15]=negzy, out=yafterzy);

    Not16(in=yafterzy, out=zafterneg);
    Mux16(a=yafterzy, b=zafterneg, sel=ny, out=ytransformed);

    // apply function
    And16(a=xtransformed, b=ytransformed, out=andout);
    Add16(a=xtransformed, b=ytransformed, out=sumout);

    Mux16(a=andout, b=sumout, sel=f, out=fout);
    Not16(in=fout, out=negfout);
    Mux16(a=fout, b=negfout, sel=no, out=tmpout);

    // zr
    And16(a=tmpout, b=true, out[0..7]=bitsright);
    And16(a=tmpout, b=true, out[8..15]=bitsleft);
    Or8Way(in=bitsright, out=rightiset);
    Or8Way(in=bitsleft, out=leftisset);
    Or(a=rightiset, b=leftisset, out=isset);
    Not(in=isset, out=zr);

    // ng
    And16(a=tmpout, b=true, out[15]=msb);
    And(a=msb, b=true, out=ng);

    // out
    And16(a=tmpout, b=true, out=out);
}